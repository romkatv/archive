#!/usr/bin/env zsh
#
# Lists the contents of archives.
#
# Authors:
#   Sorin Ionescu <sorin.ionescu@gmail.com>
#   Roman Perepelitsa <roman.perepelitsa@gmail.com>

'builtin' 'emulate' '-L' 'zsh' '-o' 'no_aliases' '-o' 'no_unset' '-o' 'err_return' '-o' 'pipe_fail'

local v OPTIND OPTARG
while getopts ":hv" opt; do
  case $opt in
    v)  v=v;;
    +v) v=;;
    \?) >&2 echo -E - "lsarchive: invalid option: $OPTARG";            return 1;;
    :)  >&2 echo -E - "lsarchive: missing required argument: $OPTARG"; return 1;;
    *h)
      >&2 echo -E - 'usage: lsarchive [{+|-}v] [archive...]'
      >&2 echo -E - ''
      >&2 echo -E - 'options:'
      >&2 echo -E - '    -v    verbose archive listing'
      return 0
    ;;
  esac
done

local archive
for archive in "${@:$OPTIND}"; do
  case $archive in
    *.tar.gz|*.tgz)
      tar tzf$v $archive;;
    *.tar.bz2|*.tbz|*.tbz2)
      tar tjf$v $archive;;
    *.tar.xz|*.txz)
      tar --xz --help &>/dev/null && tar --xz -tf$v $archive || xzcat $archive | tar tf$v -;;
    *.tar.lzma|*.tlz)
      tar --lzma --help &> /dev/null && tar --lzma -tf$v $archive || lzcat $archive | tar xf$v -;;
    *.tar)
      tar tf$v $archive;;
    *.zip|*.jar)
      unzip -l$v $archive;;
    *.rar)
      ${commands[unrar]:-${commands[rar]:-false}} ${v:-l} $archive || lsar ${v:+-l} $archive;;
    *.7z)
      7za l $archive;;
    *)
      echo -E - "lsarchive: unsupported archive type: ${(q+)archive}." >&2; return 1;;
  esac
done
