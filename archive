#!/usr/bin/env zsh
#
# Creates archive file
#
# Authors:
#   Matt Hamilton <m@tthamilton.com>
#   Roman Perepelitsa <roman.perepelitsa@gmail.com>

'builtin' 'emulate' '-L' 'zsh' '-o' 'no_aliases' '-o' 'no_unset' '-o' 'err_return' '-o' 'pipe_fail'

[[ -n ${1:-} ]] || {
  echo -E - 'usage: archive <archive> [file...]

<archive> must have one of the following extensions:
  tar.gz
  tgz
  tar.bz2
  tbz
  tbz2
  tar.xz
  txz
  tar.zlma
  tlz
  tar
  zip
  jar
  rar
  7z'
  return 1
}

local archive=$1
shift

local gzip=${commands[pigz]:-gzip}
local xz=${commands[pixz]:-xz}
local bzip2=${commands[lbzip2]:-${commands[pbzip2]:-bzip2}}

case $archive in
  *.tar.gz|*.tgz)         tar -cvf $archive --use-compress-program=$gzip  "$@";;
  *.tar.bz2|*.tbz|*.tbz2) tar -cvf $archive --use-compress-program=$bzip2 "$@";;
  *.tar.xz|*.txz)         tar -cvf $archive --use-compress-program=$xz    "$@";;
  *.tar.lzma|*.tlz)       tar -cvf $archive --lzma                        "$@";;
  *.tar)                  tar -cvf $archive                               "$@";;
  *.zip|*.jar)            zip -r   $archive                               "$@";;
  *.rar)                  rar a    $archive                               "$@";;
  *.7z)                   7za a    $archive                               "$@";;
  *)
    echo -E - "archive: unsupported archive type: ${(q+)archive}." >&2
    [[ $archive != *.(gz|bz2|xz|lzma) ]] || echo -E "Use .tar.${archive:e} instead." >&2
    return 1
  ;;
esac
